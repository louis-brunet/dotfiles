#!/bin/zsh
# https://github.com/ThePrimeagen/.dotfiles/blob/master/bin/.local/scripts/tmux-sessionizer
# 
# credit to ThePrimeagen
# requires the command fzf

# set -x
set -e

if ! which tmux; then
    echo '[tmux-sessionizer] tmux: not found'
    exit 1
fi

start_tmux() {
    TERM=tmux-256color tmux -u $@
}

if [[ $# -eq 1 ]]; then
    selected=$1
else
    if [ -n "$TMUX_SESSIONIZER_DIRS" ]; then
        typeset -T TMUX_SESSIONIZER_DIRS dirs
    else
        dirs="$PROJECTS"
    fi
    selected=$(find $dirs -mindepth 1 -maxdepth 1 -type d -not -path '*.git*' | fzf)
fi

if [[ -z $selected ]]; then
    exit 0
fi

selected_name=$(basename "$selected" | tr . _)
tmux_running=$(pgrep tmux)

if [[ -z $TMUX ]] && [[ -z $tmux_running ]]; then
    # not in tmux, tmux not running
    start_tmux new-session -s "$selected_name" -c "$selected"
    exit 0
fi

# tmux is running

if ! tmux has-session -t="$selected_name" 2> /dev/null; then
    # requested session not yet created
    start_tmux new-session -ds "$selected_name" -c "$selected"
fi

if [[ -z $TMUX ]]; then
    # not in tmux, but tmux is running
    start_tmux attach -t "$selected_name"
else
    # inside tmux
    start_tmux switch-client -t "$selected_name"
fi

# vi:ft=sh
